/*
 * Copyright (C) 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.example.android.hilt"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'com.example.android.hilt.CustomTestRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments["room.incremental"] = "true"
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions { jvmTarget = "1.8" }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify', '-ea'
                jacoco {
                    includeNoLocationClasses = true
                }
            }
            includeAndroidResources = true
        }
    }

    hilt {
        enableTransformForLocalTests = true
    }

}

dependencies {
    implementation(
            'org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version',
            'androidx.appcompat:appcompat:1.1.0',
            'androidx.core:core-ktx:1.3.0',
            'androidx.constraintlayout:constraintlayout:1.1.3',
            'androidx.recyclerview:recyclerview:1.1.0',
            // Room
            'androidx.room:room-runtime:2.2.5',

            // Hilt
            "com.google.dagger:hilt-android:$hilt_version",
            "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha01",

            // Lifecycle components
            "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version",
            "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version",
            // Navigation
            "androidx.navigation:navigation-fragment-ktx:$nav_version",
            "androidx.navigation:navigation-ui-ktx:$nav_version"
    )
    kapt(
            "androidx.lifecycle:lifecycle-compiler:$lifecycle_version",
            'androidx.room:room-compiler:2.2.5',
            "com.google.dagger:hilt-android-compiler:$hilt_version",
            "androidx.hilt:hilt-compiler:1.0.0-alpha01"
    )

    // Robolectric
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation "androidx.test:core-ktx:1.2.0"
    testImplementation "androidx.test.ext:junit-ktx:1.1.1"
    testImplementation "androidx.test:rules:1.2.0"
    testImplementation 'androidx.core:core-ktx:1.3.0'
    testImplementation "androidx.arch.core:core-testing:$arch_version"
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    testImplementation "org.mockito:mockito-core:1.10.19"
    testImplementation "org.amshove.kluent:kluent-android:1.61"
    testImplementation "junit:junit:4.13"
    debugImplementation('androidx.fragment:fragment-testing:1.1.0') {
        // exclude androidx.test:core while fragment_testing depends on 1.1.0
        exclude group: 'androidx.test', module: 'core'
    }
    kaptTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Testing dependencies
    androidTestImplementation "junit:junit:4.13"
    androidTestImplementation "androidx.test:core-ktx:1.2.0"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.1"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    androidTestImplementation "org.amshove.kluent:kluent-android:1.61"
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
}
